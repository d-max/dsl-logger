apply plugin: 'kotlin'
apply plugin: 'maven'

repositories {
    maven {
        url "https://dl.bintray.com/d-max/dsl-logger"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "com.github.d-max:dsl-logger:0.1"
    compile "org.slf4j:slf4j-api:1.7.25"
    implementation project(":common")
}

task createArtifacts

task sourcePackage(type: Jar) {
    baseName = "$rootProject.ext.artifact-slf4j"
    classifier = "sources"
    version rootProject.ext.slf4jVersion
    from sourceSets.main.kotlin.srcDirs
    destinationDir file(rootProject.ext.artifactsDir)
    createArtifacts.dependsOn it
}

task copyJar(type: Copy) {
    def filename = jar.outputs.files[0]
    from filename
    into file(rootProject.ext.artifactsDir)
    rename {
        "${rootProject.ext.artifact}-slf4j-${rootProject.ext.slf4jVersion}.jar"
    }
    dependsOn jar
    createArtifacts.dependsOn it
}

task generatePom {
    createArtifacts.dependsOn it
    doLast {
        pom {
            project {
                groupId "com.github.d-max"
                artifactId "dsl-logger-slf4j"
                version rootProject.ext.slf4jVersion
                packaging "jar"
                name "Slf4j bridge for DSL logger in Kotlin"
                description "Adapter of slf4j for logging domain-specific language implementation in Kotlin"
                url "https://github.com/d-max/dsl-logger"
                scm {
                    url "https://github.com/d-max/dsl-logger"
                    connection "scm:git@github.com:d-max/dsl-logger.git"
                    developerConnection "scm:git@github.com:d-max/dsl-logger.git"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "http://www.opensource.org/licenses/mit-license.php"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "d-max"
                        name "Maksym Dybarskyi"
                        email "maxim.dybarskyy@gmail.com"
                    }
                }
            }
        }.writeTo("$rootProject.ext.artifactsDir/${rootProject.ext.artifact}-slf4j-${rootProject.ext.slf4jVersion}.pom")
    }
}
