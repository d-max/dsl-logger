apply plugin: 'kotlin'
apply from: "../scripts/maven.gradle"

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "com.github.d-max:dsl-logger:0.1"
    compile "org.slf4j:slf4j-api:1.7.25"
}

task createArtifacts

task sourcePackage(type: Jar) {
    baseName = "$rootProject.ext.artifact-slf4j"
    classifier = "sources"
    version rootProject.ext.slf4jVersion
    from sourceSets.main.kotlin.srcDirs
    destinationDir file(rootProject.ext.artifactsDir)
    createArtifacts.dependsOn it
}

task copyJar(type: Copy) {
    def filename = jar.outputs.files[0]
    from filename
    into file(rootProject.ext.artifactsDir)
    rename {
        "${rootProject.ext.artifact}-slf4j-${rootProject.ext.slf4jVersion}.jar"
    }
    dependsOn jar
    createArtifacts.dependsOn it
}

task generatePom {
    createArtifacts.dependsOn it
    doLast {
        pom {
            def pom = delegate as org.gradle.api.publish.maven.MavenPom
            project {
                groupId "dmax.dsl"
                artifactId "logger-slf4j"
                version rootProject.ext.slf4jVersion
                packaging "jar"
                name "Slf4j bridge for DSL logger in Kotlin"
                description "Adapter of slf4j for logging domain-specific language implementation in Kotlin"
                project.ext.applyMavenAttributes.doCall(pom)
            }
        }.writeTo("$rootProject.ext.artifactsDir/${rootProject.ext.artifact}-slf4j-${rootProject.ext.slf4jVersion}.pom")
    }
}
