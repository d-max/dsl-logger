apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

repositories {
    maven {
        url "https://dl.bintray.com/d-max/dsl-logger"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api "com.github.d-max:dsl-logger:0.1"
    implementation project(":common")
}

android {

    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

task createArtifacts

task sourcePackage(type: Jar) {
    baseName = "$rootProject.ext.artifact-android"
    classifier = "sources"
    version rootProject.ext.androidVersionName
    from android.sourceSets.main.kotlin.srcDirs
    destinationDir file(rootProject.ext.artifactsDir)
    createArtifacts.dependsOn it
}

android.libraryVariants.all { variant ->
    if (variant.buildType.name == "release") {
        File outDir = file(rootProject.ext.artifactsDir)
        File apkFile = variant.outputs[0].outputFile

        def task = project.tasks.create("copy${variant.name.capitalize()}Aar", Copy)
        task.from apkFile
        task.into outDir
        task.rename {
            "${rootProject.ext.artifact}-android-${rootProject.ext.androidVersionName}.aar"
        }

        task.dependsOn variant.assemble
        createArtifacts.dependsOn task
    }
}

task generatePom {
    createArtifacts.dependsOn it
    doLast {
        pom {
            project {
                groupId "com.github.d-max"
                artifactId "dsl-logger-android"
                version rootProject.ext.androidVersionName
                packaging "jar"
                name "Android implementation of DSL logger in Kotlin"
                description "Androd verions of logging domain-specific language implementation in Kotlin"
                url "https://github.com/d-max/dsl-logger"
                scm {
                    url "https://github.com/d-max/dsl-logger"
                    connection "scm:git@github.com:d-max/dsl-logger.git"
                    developerConnection "scm:git@github.com:d-max/dsl-logger.git"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "http://www.opensource.org/licenses/mit-license.php"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "d-max"
                        name "Maksym Dybarskyi"
                        email "maxim.dybarskyy@gmail.com"
                    }
                }
            }
        }.writeTo("$rootProject.ext.artifactsDir/${rootProject.ext.artifact}-android-${rootProject.ext.androidVersionName}.pom")
    }
}
