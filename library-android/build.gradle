apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: "../scripts/maven.gradle"

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api "dmax.dsl:logger:${rootProject.ext.libraryVersion}"
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

task createArtifacts

task sourcePackage(type: Jar) {
    baseName = "$rootProject.ext.artifact-android"
    classifier = "sources"
    version rootProject.ext.androidVersionName
    from android.sourceSets.main.kotlin.srcDirs
    destinationDir file(rootProject.ext.artifactsDir)
    createArtifacts.dependsOn it
}

android.libraryVariants.all { variant ->
    if (variant.buildType.name == "release") {
        File outDir = file(rootProject.ext.artifactsDir)
        File apkFile = variant.outputs[0].outputFile

        def task = project.tasks.create("copy${variant.name.capitalize()}Aar", Copy)
        task.from apkFile
        task.into outDir
        task.rename {
            "${rootProject.ext.artifact}-android-${rootProject.ext.androidVersionName}.aar"
        }

        task.dependsOn variant.assemble
        createArtifacts.dependsOn task
    }
}

task generatePom {
    createArtifacts.dependsOn it
    doLast {
        pom {
            def pom = delegate as org.gradle.api.publish.maven.MavenPom
            project {
                groupId "dmax.dsl"
                artifactId "logger-android"
                version rootProject.ext.androidVersionName
                packaging "jar"
                name "Android implementation of writer for DSL logger in Kotlin"
                description "Androd verions of logging domain-specific language implementation in Kotlin"
                project.ext.applyMavenAttributes.doCall(pom)
                // maven plugin can't catch new android `api` dependency scope
                dependencies {
                    dependency {
                        groupId "dmax.dsl"
                        artifactId "logger"
                        version rootProject.ext.libraryVersion
                        scope "provided"
                    }
                }
            }
        }.writeTo("$rootProject.ext.artifactsDir/${rootProject.ext.artifact}-android-${rootProject.ext.androidVersionName}.pom")
    }
}
