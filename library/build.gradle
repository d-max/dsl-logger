apply plugin: 'kotlin'
apply plugin: 'maven'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

task createArtifacts

task sourcePackage(type: Jar) {
    baseName = rootProject.ext.artifact
    classifier = "sources"
    version rootProject.ext.libraryVersion
    from sourceSets.main.kotlin.srcDirs
    destinationDir file(rootProject.ext.artifactsDir)
    createArtifacts.dependsOn it
}

task copyJar(type: Copy) {
    def filename = jar.outputs.files[0]
    from filename
    into file(rootProject.ext.artifactsDir)
    rename {
        "${rootProject.ext.artifact}-${rootProject.ext.libraryVersion}.jar"
    }
    dependsOn jar
    createArtifacts.dependsOn it
}

task generatePom {
    createArtifacts.dependsOn it
    doLast {
        pom {
            project {
                groupId "com.github.d-max"
                artifactId "dsl-logger"
                version rootProject.ext.libraryVersion
                packaging "jar"
                name "DSL logger in Kotlin"
                description "Logging domain-specific language implementation in Kotlin"
                url "https://github.com/d-max/dsl-logger"
                scm {
                    url "https://github.com/d-max/dsl-logger"
                    connection "scm:git@github.com:d-max/dsl-logger.git"
                    developerConnection "scm:git@github.com:d-max/dsl-logger.git"
                }
                licenses {
                    license {
                        name "MIT License"
                        url "http://www.opensource.org/licenses/mit-license.php"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "d-max"
                        name "Maksym Dybarskyi"
                        email "maxim.dybarskyy@gmail.com"
                    }
                }
            }
        }.writeTo("$rootProject.ext.artifactsDir/${rootProject.ext.artifact}-${rootProject.ext.libraryVersion}.pom")
    }
}
